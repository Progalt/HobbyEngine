
#version 450 

#include "Defines.glsl"

layout(local_size_x = THREAD_GROUP_SIZE, local_size_y = THREAD_GROUP_SIZE) in;

layout(binding = 0, rgba16f) uniform writeonly image2D outputImage;

layout(binding = 1) uniform sampler2D inputImage;

vec2 barrelDistortion(vec2 coord, float amt) {
	vec2 cc = coord - 0.5;
	float dist = dot(cc, cc);
	return coord + cc * dist * amt;
}

float sat( float t )
{
	return clamp( t, 0.0, 1.0 );
}

float linterp( float t ) {
	return sat( 1.0 - abs( 2.0*t - 1.0 ) );
}

float remap( float t, float a, float b ) {
	return sat( (t - a) / (b - a) );
}

vec4 spectrum_offset( float t ) {
	vec4 ret;
	float lo = step(t,0.5);
	float hi = 1.0-lo;
	float w = linterp( remap( t, 1.0/6.0, 5.0/6.0 ) );
	ret = vec4(lo,1.0,hi, 1.) * vec4(1.0-w, w, 1.0-w, 1.);

	return pow( ret, vec4(1.0/2.2) );
}

const float max_distort = 0.1;
const int num_iter = 12;
const float reci_num_iter_f = 1.0 / float(num_iter);

void main()
{

	vec2 uv = (vec2(gl_GlobalInvocationID.xy) / vec2(imageSize(outputImage)).xy);

	vec4 sumcol = vec4(0.0);
	vec4 sumw = vec4(0.0);	
	for ( int i=0; i<num_iter;++i )
	{
		float t = float(i) * reci_num_iter_f;
		vec4 w = spectrum_offset( t );
		sumw += w;

		vec2 finalUV = barrelDistortion(uv, .6 * max_distort*t );
		sumcol += w * texture( inputImage,finalUV );
	}

	vec4 final = sumcol / sumw;

	imageStore(outputImage, ivec2(gl_GlobalInvocationID.xy), final);
}